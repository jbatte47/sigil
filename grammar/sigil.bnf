<program>           ::= <declaration_list>

<declaration_list>    ::= <declaration>
                     | <declaration_list> <declaration>

<declaration>         ::= <import_decl>
                     | <export_decl>
                     | <instance_decl>
                     | <incant_decl>
                     | <manifest_decl>
                     | <imbue_decl>
                     | <invoke_stat>
                     | <simulation_block>
                     | NEWLINE

<import_decl>       ::= "import" <identifier_list> "from" <identifier> ";"

<export_decl>       ::= "export" <declaration>

<instance_decl>     ::= "instance" <identifier> ":" <identifier> <instance_init> <instance_decorators> ";"
                     | "instance" <identifier> ":" <identifier> <instance_init> <instance_decorators>

<instance_init>     ::= "(" <call_arg_list> ")"
                     | /* empty */

<instance_decorators> ::= <instance_decorators> <decorator>
                        | /* empty */

<decorator>          ::= "." <identifier> "(" <call_arg_list> ")"

<incant_decl>       ::= "incant" <identifier> <opt_params> ":" <statement_block>

<opt_params>        ::= "(" <param_list> ")"
                     | "(" ")"
                     | /* empty */

<manifest_decl>      ::= "manifest" <identifier> "as" <identifier> ";"

<imbue_decl>         ::= "imbue" "into" <identifier> ":" <imbue_content>

<imbue_content>      ::= INDENT <statement_list> DEDENT
                     | NEWLINE INDENT <statement_list> DEDENT

<statement_block>   ::= INDENT <statement_list> DEDENT
                     | NEWLINE INDENT <statement_list> DEDENT
                     | <statement_list>

<statement_list>    ::= <statement>
                     | <statement_list> <statement>

<statement>         ::= "let" <identifier> "=" <expression>
                     | <expression>
                     | "yield" <expression>
                     | <gesture_stmt>
                     | NEWLINE
                     | ";"

<expression>        ::= <binary_expr>
                     | <expression> "->" <binary_expr>

<binary_expr>       ::= <term>
                     | <binary_expr> "+" <term>
                     | <binary_expr> "-" <term>

<term>              ::= <factor>
                     | <term> "*" <factor>
                     | <term> "/" <factor>

<factor>            ::= <primary_expr>
                     | "(" <expression> ")"

<primary_expr>      ::= <identifier>
                     | NUMBER
                     | STRING
                     | <primary_expr> "." <identifier>
                     | <primary_expr> "." "gesture"
                     | <primary_expr> <call_args>

<call_args>         ::= "(" <call_arg_list> ")"

<opt_call_args>     ::= <call_args>
                     | /* empty */

<call_arg_list>     ::= <call_arg>
                     | <call_arg_list> "," <call_arg>
                     | <call_arg_list> "+" <call_arg>
                     | /* empty */

<call_arg>          ::= <identifier> "=" <expression>
                     | <expression>

<member_access>     ::= <primary_expr> "." <identifier>

<simulation_block>  ::= "given" <expression> ":" <statement_block>

<gesture_stmt>      ::= "gesture" <opt_call_args> "with" <identifier> <opt_call_args> ";"
                     | "gesture" <opt_call_args> "with" <identifier> <opt_call_args>

<invoke_stat>        ::= "invoke" <identifier> ";"

<param_list>        ::= <param>
                     | <param_list> "," <param>

<param>             ::= <identifier> ":" <identifier>

<identifier_list>    ::= <identifier>
                     | <identifier_list> "," <identifier>

<type>              ::= <identifier>
<identifier>        ::= IDENTIFIER